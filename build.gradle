buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.saantiaguilera.gradle.publish.helper:core:3.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.fsryan.gradle.autosemver:autosemver-git:0.2.0-beta'
    }
}

apply plugin: 'autosemver-git'
apply plugin: 'com.saantiaguilera.gradle.publish.helper'

group = 'com.fsryan.gradle.autosemver'
long buildTime = new Date().getTime()

subprojects {
    apply plugin: 'autosemver-git'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }

    group = rootProject.group

    jacoco {
        toolVersion = "0.7.6.+"
    }

    task jacocoReport(type: JacocoReport, dependsOn: 'test') {
        group = 'Reporting'
        description = 'Generate Jacoco coverage reports after running tests.'

        executionData = files('build/jacoco/test.exec')
        sourceDirectories = files('src/main/groovy')
        classDirectories = fileTree(
                dir: 'build/classes',
                excludes: [
                        '**/*Test.class'
                ]
        )

        reports {
            xml.enabled true
            csv.enabled false
            html.enabled = true
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = rootProject.group
                artifactId = project.name
                version = project.version
                from components.java
            }
            bintray(MavenPublication) {
                groupId = rootProject.group
                artifactId = project.name
                version = project.version
                from components.java
            }
            mavenS3(MavenPublication) {
                groupId = rootProject.group
                artifactId = project.name
                version = project.version
                from components.java
                pom.withXml {
                    asNode().appendNode('build')
                        .appendNode('extensions')
                            .appendNode('extension')
                                .appendNode('groupId', 'org.kuali.maven.wagons').parent()
                                .appendNode('artifactId', 'maven-s3-wagon').parent()
                                .appendNode('version', '1.2.1')
                }
            }
        }
        repositories {
            maven {
                name 'release'
                url "s3://repo.fsryan.com/release"
                credentials(AwsCredentials) {
                    accessKey = project.hasProperty("awsMavenAccessKey") ? project.property("awsMavenAccessKey") : ""
                    secretKey = project.hasProperty("awsMavenSecretKey") ? project.property("awsMavenSecretKey") : ""
                }
            }
            maven {
                name 'snapshot'
                url "s3://repo.fsryan.com/snapshot"
                credentials(AwsCredentials) {
                    accessKey = project.hasProperty("awsMavenAccessKey") ? project.property("awsMavenAccessKey") : ""
                    secretKey = project.hasProperty("awsMavenSecretKey") ? project.property("awsMavenSecretKey") : ""
                }
            }
        }
    }

    model {
        tasks.generatePomFileForBintrayPublication {
            doFirst {
                file("$buildDir/poms").mkdirs()
            }
            destination = file("$buildDir/poms/pom-default.xml")
        }
    }

    afterEvaluate {
        tasks.findByName('publishModule').dependsOn('test')
        tasks.findByName('bintrayUpload').dependsOn('generatePomFileForBintrayPublication')
    }
}

allprojects {
    autosemver {
        branchConfigs {
            integration {
                skipCiCommitMessageSuffix = '[skip ci]'
                pullRemote = 'origin'
                pushRemote = 'origin'
                versionIncrement = "patch"
                preRelease = 'test'
                metaData = Long.toString(buildTime)
            }
            alpha { // <-- for new features
                skipCiCommitMessageSuffix = '[skip ci]'
                pullRemote = 'origin'
                pushRemote = 'origin'
                versionIncrement = "minor"
                preRelease = 'alpha'
            }
            beta { // <-- for bug fixes
                skipCiCommitMessageSuffix = '[skip ci]'
                pullRemote = 'origin'
                pushRemote = 'origin'
                preRelease = 'beta'
            }
            master {    // <-- stable version
                skipCiCommitMessageSuffix = '[skip ci]'
                pullRemote = 'origin'
                pushRemote = 'origin'
            }
        }
    }
}