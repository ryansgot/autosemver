dependencies {
    compile gradleApi()
    compile localGroovy()
    compile project(':autosemver-core')

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'    // <-- localGroovy() above with gradle 2.14.1 uses groovy 2.4.5, but spock specifies 2.4.9
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}

publishConfigurations {
    versionName = project.version
    artifactId = project.name
    packageDescription = 'gradle plugin integrating autosemver with git'
}